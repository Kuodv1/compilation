package modele.analyse;
import java_cup.runtime.*;
import modele.arbre.*;
import modele.arbre.logique.*;
import modele.arbre.mathematique.*;
import modele.arbre.BlocDinstructions;
import modele.analyse.exception.*;
 
action code {: 

	private BlocDinstructions li = new BlocDinstructions();
	private boolean boolTmp = true;
	
	public  BlocDinstructions getBlocDinstructions(){
		return li;
	}

:} ;
   
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
	StringBuilder fullError = new StringBuilder();
	int ligne,colonne;
    /* Change the method report_error so it will display the line and column of where the error
       occurred in the input as well as the reason for the error which is passed into the method
       in the String 'message'. */
    public void report_error(String message, Object info) {
   
        /* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuffer m = new StringBuffer("Erreur");
        /* Check if the information passed to the method is the same type as the type 
           java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the information in the object 
               info that is being typecasted as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   		ligne = s.left+1;
   		colonne = s.right+1;
            /* Check if the line number in the input is greater or equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuffer error message the line number of the error 
                   in the input. */
                m.append(" ligne "+(s.left+1));   
                /* Check if the column number in the input is greater or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuffer error message the column number of the 
                       error in the input. */
                    m.append(", colonne "+(s.right+1));
            }
        }
        /* Add to the end of the StringBuffer error message created in this method the message that
           was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuffer 'm', which contains an error message, out on 
           a line. */
        fullError.append(m);
    }
    /* Change the method report_fatal_error so when it reports a fatal error it will display the 
       line and column number of where the fatal error occurred in the input as well as the reason
       for the fatal error which is passed into the method in the object 'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
    	throw new ErreurSyntaxiqueException("Expression non reconnus.",ligne,colonne);
    }
    
    public  BlocDinstructions getBlocDinstructions(){
    	return action_obj.getBlocDinstructions();
    }

:};
/* ------------Declaration of Terminals and Non Terminals Section----------- */
/* Terminals (tokens returned by the scanner).  
   Terminals that have no value are listed first and then terminals that do have a value, in this 
   case a String value, are listed on the next line down. */

terminal Symbol    POINTVIRGULE, VIRGULE, PAROUV, PARFER;
terminal String    PLUS, MOINS, MULT, DIV, SUP, INF, EGALEEGALE,EGALE, DIFF, AND, OR, XOR, NOT, IDF, CSTE, BOOL, IDFCLASSE, FIN, CSTECHAINE, PUBLIQUE, PRIVEE, TYPE;
terminal String		IDFLIRE,IDFECRIRE;

   
/* Non terminals used in the grammar section.  
   Non terminals that have an object value are listed first and then non terminals that have an 
   integer value are listed.  An object value means that it can be any type, it isn't set to a 
   specific type.  So it could be an Integer or a String or whatever. */

non terminal ArbreAbstrait SYSTEME, CLASSE;
non terminal Instruction LIRE,ECRIRE,DECLARATION, AUTRE_DECL, DECL_CHAMP, DECL_CONST, AFFECT;
non terminal Expression EXPR;
non terminal String STATUT;

precedence left SUP, INF, EGALEEGALE, DIFF, AND, OR, XOR, NOT;
precedence left PLUS, MOINS;
precedence left MULT, DIV;

/* ----------------------------Grammar Section-------------------- */

start with SYSTEME;

SYSTEME	::=	CLASSE;

CLASSE ::= IDFCLASSE:idfc IDF DECLARATION FIN
		{:
			li.ajouter(new FermerBloc(idfcleft+1,idfcright+1));
		:}| IDFCLASSE:idfc IDF FIN
		{:
			li.ajouter(new FermerBloc(idfcleft+1,idfcright+1));
		:};

DECLARATION ::= DECLARATION DECL_CHAMP:dc
				{:
				:}
				| DECL_CHAMP:dc
				{:
					li.ajouter(new OuvrirBloc(dcleft+1,dcright+1));
				:}
				| DECLARATION DECL_CONST:dc
				{:
					li.ajouter(dc);
				:}
				| DECL_CONST:dc
				{:
					li.ajouter(new OuvrirBloc(dcleft+1,dcright+1));
					li.ajouter(dc);
				:};
				
DECL_CHAMP ::= STATUT TYPE:t IDF:idf POINTVIRGULE
			{:
				li.ajouter(new Declaration(idf,boolTmp,idfleft+1, idfright+1));
			:}
			| STATUT TYPE:t IDF:idf AUTRE_DECL POINTVIRGULE
			{:
				li.ajouter(new Declaration(idf,boolTmp,idfleft+1, idfright+1));
			:};

AUTRE_DECL ::= VIRGULE IDF:idf
			{:
				li.ajouter(new Declaration(idf,false,idfleft+1,idfright+1));
			:}
			|AUTRE_DECL VIRGULE IDF:idf
			{:
				li.ajouter(new Declaration(idf,false, idfleft+1, idfright+1));
			:};

STATUT ::= PUBLIQUE
			{:
				boolTmp = false;
				
			:}
			| PRIVEE
			{:
				boolTmp = true;
			:};

DECL_CONST ::= AFFECT:a
			{:
				RESULT = a;
			:}
			| LIRE:l
			{:
				RESULT = l;
			:}
			| ECRIRE:e
			{:
				RESULT = e;
			:};
			
AFFECT ::= IDF:i EGALE EXPR:e POINTVIRGULE
			{:
				RESULT = new Affectation(i,e,ileft+1,iright+1);
			:};
			
LIRE ::= IDFLIRE IDF:idf POINTVIRGULE
		{:
			RESULT = new Lire(idf,idfleft+1,idfright+1);
		:};

ECRIRE ::= IDFECRIRE EXPR:e POINTVIRGULE
		{:
			RESULT = new EcrireExpression(e,eleft+1,eright+1);
		:}
		| IDFECRIRE CSTECHAINE:chaine POINTVIRGULE
		{:
			li.increJump();
			RESULT = new Ecrire(chaine,li.getJump(),chaineleft+1,chaineright+1);
		:};

        
EXPR ::= EXPR:e1 PLUS:o EXPR:e2
         {:
         	if(!e1.getIsBool()) {
				RESULT = new Plus(e1,e2,e1left+1,e1right+1);
			} else {
				RESULT = new Ou(e1,e2,e1left+1,e1right+1);
			}
         :} |
         EXPR:e1 MOINS:o EXPR:e2
         {:
         	if(!e1.getIsBool()) {
				RESULT = new Moins(e1,e2,e1left+1,e1right+1);
			} else {
				RESULT = new Xor(e1,e2,e1left+1,e1right+1);
			}
         :} |
         MOINS:o EXPR:e
         {:
         	RESULT = new MoinsUnaire(e,eleft+1,eright+1);
         :} |
         EXPR:e1 MULT:o EXPR:e2
         {:
         	if(!e1.getIsBool()) {
				RESULT = new Mult(e1,e2,e1left+1,e1right+1);
			} else {
				RESULT = new Et(e1,e2,e1left+1,e1right+1);
			}
         :} |
         EXPR:e1 DIV:o EXPR:e2
         {:
			RESULT = new Div(e1,e2,e1left+1,e1right+1);
         :} |
         EXPR:e1 SUP:o EXPR:e2
         {:
         	RESULT = new Superieure(e1,e2,e1left+1,e1right+1);
         :} |
         EXPR:e1 INF:o EXPR:e2
         {:
         	RESULT = new Inferieure(e1,e2,e1left+1,e1right+1);
         :} |
         EXPR:e1 EGALEEGALE:o EXPR:e2
         {:
         	RESULT = new Egalite(e1,e2,e1left+1,e1right+1);
         :} |
         EXPR:e1 DIFF:o EXPR:e2
         {:
         	RESULT = new Different(e1,e2,e1left+1,e1right+1);
         :} |
         NOT:o EXPR:e
         {:
         	RESULT = new Not(e,eleft+1,eright+1);
         :} |
         PAROUV EXPR:e PARFER
         {:
         	RESULT = e;
         :} |
		 CSTE:c 
	     {:
			RESULT = new Nombre(c,cleft+1,cright+1);
         :} |
         BOOL:t
         {:
         	RESULT = new Booleen(t,tleft+1,tright+1);
         :} |
         IDF:idf
         {:
         	RESULT = new Variable(idf,idfleft+1,idfright+1);
         :}
         ;
