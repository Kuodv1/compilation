package modele.analyse ;
import java_cup.runtime.*;
import modele.arbre.*;
 
action code {: 


:} ;
   
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
	StringBuilder fullError = new StringBuilder();
    /* Change the method report_error so it will display the line and column of where the error
       occurred in the input as well as the reason for the error which is passed into the method
       in the String 'message'. */
    public void report_error(String message, Object info) {
   
        /* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuffer m = new StringBuffer("Erreur");
    
        /* Check if the information passed to the method is the same type as the type 
           java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the information in the object 
               info that is being typecasted as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuffer error message the line number of the error 
                   in the input. */
                m.append(" ligne "+(s.left+1));   
                /* Check if the column number in the input is greater or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuffer error message the column number of the 
                       error in the input. */
                    m.append(", colonne "+(s.right+1));
            }
        }
        /* Add to the end of the StringBuffer error message created in this method the message that
           was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuffer 'm', which contains an error message, out on 
           a line. */
        //System.err.println(m);
        fullError.append(m);
    }
    /* Change the method report_fatal_error so when it reports a fatal error it will display the 
       line and column number of where the fatal error occurred in the input as well as the reason
       for the fatal error which is passed into the method in the object 'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
    	throw new RuntimeException(fullError.toString());
        //report_error(message, info);
        //System.exit(1);
    }
:};
/* ------------Declaration of Terminals and Non Terminals Section----------- */
/* Terminals (tokens returned by the scanner).  
   Terminals that have no value are listed first and then terminals that do have a value, in this 
   case a String value, are listed on the next line down. */

terminal Symbol    POINTVIRGULE, PAROUV, PARFER;
terminal String    PLUS, MOINS, MULT, DIV, IDF, CSTE;

   
/* Non terminals used in the grammar section.  
   Non terminals that have an object value are listed first and then non terminals that have an 
   integer value are listed.  An object value means that it can be any type, it isn't set to a 
   specific type.  So it could be an Integer or a String or whatever. */

non terminal ArbreAbstrait PROG, INST, LINST;
non terminal Expression EXPR;

precedence left PLUS, MOINS;
precedence left MULT, DIV;

/* ----------------------------Grammar Section-------------------- */

start with PROG ;

PROG	::=	LINST:li
		{: RESULT = li; :}
		;

LINST	::=	LINST:li INST:i
		{: 
		   li.ajouter(i);
		   RESULT = li;
		:}
	|	INST:i
		{: 
			BlocDinstructions li = new BlocDinstructions();
			li.ajouter(i);
		  	 RESULT = li;
		:}
		;

INST ::= EXPR:e
		 {: 
			RESULT = e;
		 :} 
		 ;

        
EXPR ::= EXPR:e1 PLUS:o EXPR:e2
         {:
			RESULT = new Plus(e1,e2);
         :} |
         EXPR:e1 MOINS:o EXPR:e2
         {:
			RESULT = new Moins(e1,e2);
         :} |
         EXPR:e1 MULT:o EXPR:e2
         {:
			RESULT = new Mult(e1,e2);
         :} |
         EXPR:e1 DIV:o EXPR:e2
         {:
			RESULT = new Div(e1,e2);
         :} |
         PAROUV EXPR:e PARFER
         {:
         	RESULT = e;
         :} |
		 CSTE:c 
	     {:
			RESULT = new Nombre(c);
         :}
         ;
